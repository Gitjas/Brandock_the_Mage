/* Cleanup in Flaming Fist Headquraters in case PC walks in in Chapter 7 while still being chased as murder suspect and Dilos et al turned hostile

only necessary if EndlessBG1 isn't installed */

ACTION_IF NOT FILE_EXISTS_IN_GAME ~c#stdst2.bcs~ THEN BEGIN

<<<<<<<< .../brando_inlined_cleanup_more.d
/* Benjy and Dilos in FF Headquarters: do not turn hostile after scene in Ducal Palace */
REPLACE_STATE_TRIGGER BENJY 0 ~Global("DukeThanks","GLOBAL",0)~
ADD_STATE_TRIGGER FLAMBG 5 ~Global("DukeThanks","GLOBAL",0)~

>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../brando_inlined_cleanup_more.d~

/* Benjy and Dilos in FF Headquarters: turn friendly after scene in Ducal Palace */
<<<<<<<< .../brando_inlined_ar0706_add.baf
IF
	Exists("BENJY")  // Benjy
	GlobalGT("DukeThanks","GLOBAL",0)
	Allegiance("BENJY",ENEMY)
THEN
	RESPONSE #100
		ActionOverride("BENJY",ChangeEnemyAlly(Myself,NEUTRAL))
END

IF
	Exists("DICKSMOK")  // Dilos
	GlobalGT("DukeThanks","GLOBAL",0)
	Allegiance("DICKSMOK",ENEMY)
THEN
	RESPONSE #100
		ActionOverride("DICKSMOK",ChangeEnemyAlly(Myself,NEUTRAL))
END
>>>>>>>>
EXTEND_BOTTOM ~%SWBaldursGate_FlamingFistHQ_L1%.bcs~ ~.../brando_inlined_ar0706_add.baf~

/* define script for cres without DV: DestroySelf() after Sarevok's demasking in the Ducal Palace */
<<<<<<<< .../c#stdst2.baf

IF
	GlobalGT("DukeThanks","GLOBAL",0)
THEN
	RESPONSE #100
        	DestroySelf()
END

>>>>>>>>

COMPILE EVALUATE_BUFFER ~.../c#stdst2.baf~

/* in case FLAMANG.cre was spawned in Flaming Fist HQ (hostile FF mercenaries): make them despawn after Sarevok is demasked */
COPY_EXISTING ~FLAMANG.cre~ ~override~
  WRITE_EVALUATED_ASCII 0x260 ~c#stdst2~ #8 // General Script 

END