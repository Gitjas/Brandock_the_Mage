/* script to "Elvenhair's desk" C#BREDSK.baf */

/* not in chapter 7 if searched for murder */
IF
	Clicked([ANYONE])
	InMyArea(Player1)
	InMyArea("firebe")
	!StateCheck("firebe",CD_STATE_NOTVALID)
	InMyArea("C#Brandock")
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck("C#Brandock",CD_STATE_NOTVALID)
	GlobalGT("C#Br_BookRestore","GLOBAL",0)
	Global("Chapter","GLOBAL",%tutu_chapter_7%)
	Global("DukeThanks","GLOBAL",0)
	Global("C#Brando_BookquestHold","GLOBAL",0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		ActionOverride("firebe",StartDialogNoSet("C#Brandock"))
		SetInterrupt(TRUE)
END

/* PC is no longer serched for murder */
IF
	Clicked([ANYONE])
	InMyArea(Player1)
	InMyArea("firebe")
	!StateCheck("firebe",CD_STATE_NOTVALID)
	InMyArea("C#Brandock")
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck("C#Brandock",CD_STATE_NOTVALID)
	GlobalGT("C#Br_BookRestore","GLOBAL",0)
	Global("DukeThanks","GLOBAL",1)
	GlobalLT("C#Brando_BookquestHold","GLOBAL",2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		ActionOverride("firebe",StartDialogNoSet("C#Brandock"))
		SetInterrupt(TRUE)
END

/* Brandock looks at the desk for the first time */
IF
	Clicked([ANYONE])
	InMyArea(Player1)
	InMyArea("firebe")
	!StateCheck("firebe",CD_STATE_NOTVALID)
	InMyArea("C#Brandock")
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck("C#Brandock",CD_STATE_NOTVALID)
	Global("C#Br_LorePathElvenhair","GLOBAL",0)
	!Global("C#Brando_BookquestHold","GLOBAL",1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		DisplayString(Myself,@1038)
		Wait(2)
		SetGlobal("C#Br_LorePathElvenhair","GLOBAL",1)
		SetInterrupt(TRUE)
END


IF
	Clicked([ANYONE])
	InMyArea(Player1)
	InMyArea("firebe")
	!StateCheck("firebe",CD_STATE_NOTVALID)
	InMyArea("C#Brandock")
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	Global("C#Br_ScribeScrollQuest","GLOBAL",1)
	!Global("C#Brando_BookquestHold","GLOBAL",1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("C#Br_ScribeScrollQuest","GLOBAL",2)
		SetInterrupt(TRUE)
END

IF
	Clicked([ANYONE])
	InMyArea(Player1)
	!Dead("firebe")
	InMyArea("C#Brandock")
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	Global("C#Br_ScribeScrollQuest","GLOBAL",4)
	!Global("C#Brando_BookquestHold","GLOBAL",1)
	OR(2)
		Global("c#brscrl","GLOBAL",0)
		Global("c#brscrl","GLOBAL",3)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		IncrementGlobal("c#brscrl","GLOBAL",1)
		SetInterrupt(TRUE)
END



/* If no other conditions are true */
IF
	Clicked([ANYONE])
THEN
	RESPONSE #100
		DisplayString(Myself,@1039)
END