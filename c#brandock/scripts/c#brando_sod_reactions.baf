/* scry pool scene. Wasn't it Hephernaan, who...? */

/* bd1200 Lich scrying pool - detect whether Brandock witnessed the scene with Hephernaan */

/* Detection: Brandock is in area */
IF
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	CombatCounter(0)
	!See([ENEMY])
	!Dead("C#Brandock")
	AreaCheck("bd1200")
	Global("C#Brandock_HepherScryPool","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_HepherScryPool","GLOBAL",1)
END

/* reset variable: Brandock is not in area or can't watch the scene */
IF
	OR(3) 
		Global("C#BrandockJoined","GLOBAL",0)
		Dead("C#Brandock")
		!AreaCheck("bd1200")
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_HepherScryPool","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_HepherScryPool","GLOBAL",0)
END

/* activation */
IF
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	!RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_HephernaanIdentity","GLOBAL",1) //knows who Hephernaan is from RtD
		Global("C#RtD_RoadToDiscovery","GLOBAL",0) //RtD is not installed
	OR(3)
		Global("C#RtD_HephernaanIdentity","GLOBAL",1)
		GlobalGT("bd_plot","global",169) //after seeing Hephernaan at the parley at Coastway Crossing
		Global("C#RtD_RoadToDiscovery","GLOBAL",1) //RtD is installed
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#Brandock_HepherScryPool","GLOBAL",2) //NPC watched the scene
THEN 
	RESPONSE #100
		SetGlobal("C#Brandock_HepherScryPool","GLOBAL",3)
END
/* initiate */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#Brandock_HepherScryPool","GLOBAL",3)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END


/* Hephernaan is working for a Fiend - general reaction */
IF
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	!RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	OR(3) //not inside castle if crusaders still hold it
		!AreaCheck("bd4300")
		GlobalLT("bd_plot","GLOBAL",315)
		GlobalGT("bd_plot","GLOBAL",380)
	OR(2) //not inside the crusader camp near entrance 
		!AreaCheck("bd5100")
		!Range("TranBD5000",35) 
	!AreaCheck("bd5200")
	OR(2) //not inside the crusader camp near entrance 
		!AreaCheck("bd5000")
		!Range("TranBD5100",35) 
	Global("C#Brandock_HephernaanFiend","GLOBAL",0) 
THEN 
	RESPONSE #100
		SetGlobal("C#Brandock_HephernaanFiend","GLOBAL",1)
END
/* initiate */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#Brandock_HephernaanFiend","GLOBAL",1)
	CombatCounter(0)
	!See([ENEMY])
	See(Player1)
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END

/* General reaction to Hephernaan's betrayal */
/* this will play directly AFTER the betrayal happened. */
IF
/* activation */
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!InMyArea("bdcaelar")
	GlobalGT("bd_plot","global",494) //after Hephernaan's betrayal
	AreaCheck("bd4300")
	Global("C#Brandock_HephernaanBetrayal","GLOBAL",0) 
THEN 
	RESPONSE #100
		SetGlobal("C#Brandock_HephernaanBetrayal","GLOBAL",1)
END
/* initiation */
IF
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Brandock_HephernaanBetrayal","GLOBAL",1)	
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMERSHORT_SOD) //short!
		StartDialogNoSet(Player1)
END


/* 2.1 Conclude that Caelar's ulterior motive is to save her uncle: After knowing that she is planning on freeing souls from Avernus and knowing that Aun went to Avernus for her. */
/* activation */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_HephernaanIdentity","GLOBAL",1)
		Global("C#RtD_RoadToDiscovery","GLOBAL",0)
	OR(3)
		Global("C#RtD_HephernaanIdentity","GLOBAL",1)
		GlobalGT("bd_plot","global",169) //after seeing Hephernaan at the parley at Coastway Crossing
		Global("C#RtD_RoadToDiscovery","GLOBAL",1)
	OR(4)
		GlobalGT("C#RtD_KnowsAunArgent","GLOBAL",3)
		Global("#L_SoDStat_DaustonTalk","GLOBAL",3) //from Themed Tweaks
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",4)
		GlobalGT("C#Brandock_SoDEnd","GLOBAL",1) //after the return from Avernus
	GlobalLT("bd_plot","global",485) //before the portal is opened Avernus
	Global("C#Brandock_AllForAun","GLOBAL",0) 
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_AllForAun","GLOBAL",2) //"1" is used in I_C_T
END
/* initiation */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Brandock_AllForAun","GLOBAL",2)	
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END


/* found treatise bdbook11 */
/* activate */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	RealGlobalTimerExpired("C#BrandockDialogTimer","GLOBAL")
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!Detect([ENEMY])
	CombatCounter(0)
	PartyHasItem("bdbook11")
	Global("C#Brandock_SoD_Foundbdbook11","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		SetGlobal("C#Brandock_SoD_Foundbdbook11","GLOBAL",1)
END
/* initiate */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#Brandock_SoD_Foundbdbook11","GLOBAL",1)
	CombatCounter(0)
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END

/* Incident at Boareskyr Bridge. */
/* after the sign incident at the Boareskyr bridge */
/* activate - no The Boareskyr Bridge Scene mod installed */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !See([ENEMY])
  See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
	GlobalGT("bd_plot","global",294) //after it happened
	GlobalLT("chapter","global",10) // not if game went on
	AreaCheck("bd2000")
	!InMyArea("bdireni")
	!InMyArea("bdfist24")
	Global("C#Brandock_c#stff24_10","GLOBAL",0) // no interjection from tbbs
	Global("C#Brando_SoDBoareskyrBridgeScene","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brando_SoDBoareskyrBridgeScene","GLOBAL",1)
END

/* initiate */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !See([ENEMY])
  See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  Global("C#Brando_SoDBoareskyrBridgeScene","GLOBAL",1)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
    StartDialogNoSet(Player1)
END

/* after the sign incident at the Boareskyr bridge */
/* activate - in case The Boareskyr Bridge mod run */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	!See([ENEMY])
	See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	GlobalGT("bd_plot","global",294) //after it happened
	OR(2)
		Global("C#Brando_SoDBoareskyrBridgeScene","GLOBAL",2) //quick reaction line run
		Global("C#Brandock_c#stff24_10","GLOBAL",1) // interjection from tbbs run
	GlobalLT("C#Brando_SoDBoareskyrBridgeScene","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#Brando_SoDBoareskyrBridgeScene","GLOBAL",3)
END



/* initiate */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !See([ENEMY])
  See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  Global("C#Brando_SoDBoareskyrBridgeScene","GLOBAL",3)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
    StartDialogNoSet(Player1)
END




/* 5.1 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND Hephernaan is working for a fiend. */
/* activation */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#RtD_CaelarPlan","GLOBAL",3)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
	GlobalLT("C#RtD_CaelarPlan","GLOBAL",4)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#Brandock_SoD_PortalConclusion","GLOBAL",0)
	Global("C#SoD_PortalConclusion_1","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_SoD_PortalConclusion","GLOBAL",1) 
END
/* initiation block see below! */

/* 5.2 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND that there is a dormant rift below Dragonspear Castle. */
/* activation */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#RtD_RoadToDiscovery","GLOBAL",0)
	OR(2)
		GlobalGT("bd_plot","global",169) //after CWC Parley with Caelar
		Global("C#RtD_RoadToDiscovery","GLOBAL",1)
	OR(3)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",1)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2)
		Global("C#Brandock_SoD_Foundbdbook11","GLOBAL",1)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#Brandock_SoD_PortalConclusion","GLOBAL",0)
	Global("C#SoD_PortalConclusion_2","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_SoD_PortalConclusion","GLOBAL",2) 
END
/* initiation block see below! */

/* 5.3 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND that PC's blood is needed to open portal below Dragonspear Castle. */
/* triggers with Road to Discovery or if NPC conlcuded that Caelar needs PC's blood for portal *//* activation */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#RtD_CaelarPlan","GLOBAL",3)
	OR(2)
		GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2)
		Global("C#Brandock_PortalPCBloodConclusion","GLOBAL",3)
	GlobalLT("C#RtD_CaelarPlan","GLOBAL",4)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#Brandock_SoD_PortalConclusion","GLOBAL",0)
	Global("C#SoD_PortalConclusion_3","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_SoD_PortalConclusion","GLOBAL",3) 
END

/* Initiation for all three conclusion possibilities. Appropriate dialogue will be triggered via variable value set in the activation block. */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#Brandock_SoD_PortalConclusion","GLOBAL",0)
	GlobalLT("C#Brandock_SoD_PortalConclusion","GLOBAL",4)	
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END


/* 5.4 General reaction: Caelar wants to open a portal to Avernus. */
/* know about open portal to Avernus */
IF
  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Myself,CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  GlobalLT("bd_plot","global",485) //not after Heph's betrayal
  OR(4)
	GlobalGT("C#RtD_CaelarPlan","GLOBAL",3) //from Road to Discovery
	Global("C#Brandock_SoD_PortalConclusion","GLOBAL",4) //Brandock concluded it
  	GlobalGT("bd_plot","global",390) //after meeting with Caelar on Dead Man's Pass
	Global("C#RtD_RoadToDiscovery","GLOBAL",1) //from Road to Discovery
  OR(3)
	GlobalGT("C#RtD_CaelarPlan","GLOBAL",3) //from Road to Discovery
	Global("C#Brandock_SoD_PortalConclusion","GLOBAL",4) //Brandock concluded it
	Global("C#RtD_RoadToDiscovery","GLOBAL",0) //no Road to Discovery
  Global("C#Brandock_PortalAvernus","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("C#Brandock_PortalAvernus","GLOBAL",1)
END
/* trigger */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Brandock_PortalAvernus","GLOBAL",1)	
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END


/* 6. Conclusion that Caelar didn't want to kill the PC but only kidnap after hearing about weak poison. */
/* will only trigger with Road to Discovery */
/* activation */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#RtD_WeakPoison","GLOBAL",1)
	Global("C#RtD_CaelarWantsPC","GLOBAL",0)
	Global("C#RtD_CaelarKidnap","GLOBAL",0)
	Global("#L_SoDStat_WeakPoison","GLOBAL",0)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#Brandock_SoDKidnapConclusion","GLOBAL",0)
	Global("C#SoD_KidnapConclusion","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_SoDKidnapConclusion","GLOBAL",1)
END
/* initiation */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Brandock_SoDKidnapConclusion","GLOBAL",1)	
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END


/* 7.1 Conclusion that Caelar needs the PC's blood to open the portal: if Caelar needs Bhaalblood to open portal AND PC knows that she is no Bhaalspawn herself. */
/* this can trigger without Road to Discovery if treatise was found and NPC concluded that Caelar wants to open a portal. */
IF 
  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2) 
		GlobalGT("C#Brandock_SoD_Foundbdbook11","GLOBAL",0)
	OR(2)
		GlobalGT("C#RtD_CaelarBhaalChild","GLOBAL",1)
		GlobalGT("bd_mdd018","global",0)
	OR(2)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#RtD_RoadToDiscovery","GLOBAL",0)
	OR(3)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#Brandock_SoD_PortalConclusion","GLOBAL",4)
		Global("C#RtD_RoadToDiscovery","GLOBAL",1)
	GlobalLT("C#RtD_WantBhaalBlood","GLOBAL",2)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",3)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#Brandock_PortalPCBloodConclusion","GLOBAL",0)
	Global("C#SoD_PortalPCBloodConclusion_1","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_PortalPCBloodConclusion","GLOBAL",1)
END
/* initiation */
IF
  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Brandock_PortalPCBloodConclusion","GLOBAL",1)	
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END

/* 7.2 Conclusion that Caelar needs the PC's blood to open the portal: if Caelar needs Bhaalblood AND wanted to kidnap the PC. */
/* will only trigger with Road to Discovery variables. */
/* activation */
IF 
  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2) 
		GlobalGT("C#Brandock_SoD_Foundbdbook11","GLOBAL",1)
	OR(3)
		Global("C#RtD_WantBhaalBlood","GLOBAL",1)
		Global("C#RtD_CaelarKidnap","GLOBAL",1)
		GlobalGT("C#RtD_CaelarWantsPC","GLOBAL",1)
	OR(2)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#Brandock_SoD_PortalConclusion","GLOBAL",4)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",3)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#Brandock_PortalPCBloodConclusion","GLOBAL",0)
	Global("C#SoD_PortalPCBloodConclusion_2","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_PortalPCBloodConclusion","GLOBAL",2)
END
/* initiation */
IF
  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Brandock_PortalPCBloodConclusion","GLOBAL",2)	
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END




/* Conclusion that Caelar is no child of Bhaal: after PC reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] AND hearing rumors that she is a child of Bhaal (or the portal would already be open / she wouldn't go for the PC.) */
/* will only trigger with Road to Discovery because there is no other ingame trigger to check whether the PC heard the rumor. */
/* activation */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2)
	Global("C#RtD_CaelarBhaalChild","GLOBAL",1)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#Brandock_CaelarNoBhaalChild","GLOBAL",0) 
	Global("C#SoD_CaelarNoBhaalChild","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_CaelarNoBhaalChild","GLOBAL",1) 
END
/* initiation */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Brandock_CaelarNoBhaalChild","GLOBAL",1)	
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END

/* Reaction to the knowledge that Caelar is no Bhaalchild. */
/* Caelar no child of Bhaal */
IF
  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !RealGlobalTimerNotExpired("C#BrandockDialogTimer","GLOBAL")
  !See([ENEMY])
  See(Player1) 
  !StateCheck(Myself,CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
  OR(3)
  	GlobalGT("bd_mdd018","global",0) //Caelar told she is no child of Bhaal
	GlobalGT("C#RtD_CaelarBhaalChild","GLOBAL",1) //from Road to Discovery
  	Global("C#Brandock_CaelarNoBhaalChild","GLOBAL",2)
  GlobalLT("C#Brandock_CaelarNoBhaalChild","GLOBAL",3)
THEN
  RESPONSE #100
    SetGlobal("C#Brandock_CaelarNoBhaalChild","GLOBAL",3)
END
/* trigger */
IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#Brandock_CaelarNoBhaalChild","GLOBAL",3)	
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMER_SOD)
		StartDialogNoSet(Player1)
END