

/* Brandock initiates dialogue upon first meeting in Ebrel's house */

IF
	Global("C#BrandockSpawn","GLOBAL",10)
	AreaCheck("C#BR03")
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#BrandockSpawn","GLOBAL",11)
END

/* initiate */
IF 
Global("C#BrandockSpawn","GLOBAL",11) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END



/* "Been in Party" check: SoA */

IF
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	GlobalLT("C#Brandock_BeenInParty","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_BeenInParty","GLOBAL",2)
END

/* after stepping outside the house for the first time: Brandock talks about the sphere */

/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	AreaCheck("AR0400") //Slums
	Global("C#Brandock_SlumSphere","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_SlumSphere","GLOBAL",1)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_SlumSphere","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* After entering Government District for first time */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	AreaCheck("AR1000")
	Global("C#BrandockAboutTeos","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#BrandockAboutTeos","GLOBAL",1)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#BrandockAboutTeos","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* PC kept the bookcase (BGT / EET) */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!AreaCheck("C#BR03") //Ebrel's Home
	!AreaCheck("AR0400") //Slums
	Global("C#Brandock_BGTTOBG2","GLOBAL",4)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_BGTTOBG2","GLOBAL",5)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_BGTTOBG2","GLOBAL",5)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END


/* In case PC rejects Tolgerias' quest */

/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!See("TOLGER")
	Global("C#Brandock_TOLGER_refused","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_TOLGER_refused","GLOBAL",2)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_TOLGER_refused","GLOBAL",2)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END


/* PC killed Valygar but didn't give the body to Tolgerias AND/OR went to sphere entrance first */

/* Activate: PC refused to give Valygar's body to Tolgerias */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Dead("Valygar") 
	Global("BetrayedCowled","GLOBAL",1)
	OR(2)
		Global("C#Brandock_TOLGER_29","GLOBAL",1)
		Global("C#Brandock_TOLGER_40","GLOBAL",1)
	Global("C#Brandock_BetrayedCowled","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_BetrayedCowled","GLOBAL",1)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_BetrayedCowled","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* Activate: PC refused to tell about Valygar's hideout to Tolgerias. Valygar not in party */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!Dead("Valygar") 
	Global("BetrayedCowled","GLOBAL",1)
	OR(2)
		Global("C#Brandock_TOLGER_29","GLOBAL",1)
		Global("C#Brandock_TOLGER_40","GLOBAL",1)
	Global("C#Brandock_BetrayedCowled","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_BetrayedCowled","GLOBAL",3)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_BetrayedCowled","GLOBAL",3)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END



/* Valygar was killed in his cabin */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Dead("Valygar")
	AreaCheck("AR1101")
	Global("C#Brandock_VALYGAR_0","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_VALYGAR_0","GLOBAL",2)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_VALYGAR_0","GLOBAL",2)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* Valygar is in group */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	InParty("VALYGAR") InMyArea("VALYGAR") !StateCheck("VALYGAR",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	GlobalLT("C#Brandock_VALYGAR_0","GLOBAL",6)
	!Dead("TOLGER")
	!Dead("TOLGER2")
//	## //Sphere quest not finished yet
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_VALYGAR_0","GLOBAL",6)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	InParty("VALYGAR") InMyArea("VALYGAR") !StateCheck("VALYGAR",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_VALYGAR_0","GLOBAL",6)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* In case CHARNAME let Valygar go after meeting him in his hut - Global("HiredByCowled","GLOBAL",1) */

/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!InParty("Valygar")
	!See("Valygar")
	!Dead("Valygar")
	AreaCheck("AR1101")
	Global("HiredByCowled","GLOBAL",1)
	Global("C#Brandock_VALYGAR_0","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_VALYGAR_0","GLOBAL",4)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_VALYGAR_0","GLOBAL",4)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* PC handed Valygar over - one way or the other */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	AreaCheck("AR1000")
	Global("BetrayedValygar","GLOBAL",1)
	Global("C#Brandock_TOLGER_76","GLOBAL",1)
	Global("C#Brandock_BetrayedValygar","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_BetrayedValygar","GLOBAL",1)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_BetrayedValygar","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END



/* Leaving the Coucellor's Building, after asking around (Tolgerias quest either accepted or refused, but sphere quest not finished yet */

/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	AreaCheck("AR1000")
	Global("C#Brandock_AskedCouncBuild","GLOBAL",0)
	OR(2)
		Global("C#Brandock_CORNEIL_0","GLOBAL",1)
		Global("C#Brandock_TOLGER_0","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_AskedCouncBuild","GLOBAL",1)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_AskedCouncBuild","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END


/* spawn Teos in case Valygar was handed to the Cowled Wizards */

IF	
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck("C#Brandock",CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!Dead("MGTEOS01")  // Teos
	OR(5)
		AreaCheck("AR1000")
		AreaCheck("AR0400")
		AreaCheck("AR0411")
		AreaCheck("AR0300")
		AreaCheck("AR0700")
	GlobalTimerExpired("C#Brandock_TeosSpawnTimer","GLOBAL")
	Global("C#Brandock_TeosMeeting","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_TeosMeeting","GLOBAL",1)
		StartCutSceneMode()
		StartCutScene("cut33b") //spawn Teos
END


/* Tolgerias in the Planar Sphere dead - TOLGER2 */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	AreaCheck("AR0412")
	Dead("TOLGER2")
	Global("C#Brandock_Killed Tolgerias","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_Killed Tolgerias","GLOBAL",1)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_Killed Tolgerias","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END


/* Tolgerias and Lavok dead. Spawn Teos if PC doesn't get sphere stronghold. compatibility with NPC strongholds */
IF	
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck("C#Brandock",CD_STATE_NOTVALID)
	//## eine Abfrage, ob Brandock beim Spherenquest dabei war!
	Global("PCSphere","GLOBAL",0)
	Global("KishMultipleMages","GLOBAL",0)
	Global("KishAerieMageStronghold","GLOBAL",0)
	Global("KishEdwinMageStronghold","GLOBAL",0)
	Global("KishImoenMageStronghold","GLOBAL",0)
	Global("KishJanMageStronghold","GLOBAL",0)
	Global("KishNaliaMageStronghold","GLOBAL",0)
	Dead("LAVOK02")
	!Dead("MGTEOS01")  // Teos
	OR(5)
		AreaCheck("AR1000")
		AreaCheck("AR0400")
		AreaCheck("AR0411")
		AreaCheck("AR0300")
		AreaCheck("AR0700")
	OR(4)
		!InParty("Valygar")
		!InMyArea("Valygar")
		StateCheck("Valygar",CD_STATE_NOTVALID)
		GlobalGT("LavokDead","GLOBAL",0) //Valygar Lavok follow-up done
	Global("C#Brandock_TeosMeeting","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_TeosMeeting","GLOBAL",1) //##change this if variable is used for dialogue in case Valygar is dead
		StartCutSceneMode()
		StartCutScene("cut33b") //spawn Teos
END



/* conclusion of planar sphere quest. After Teos appeared */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!See("MGTEOS01")
	Global("C#Brandock_MGTEOS01_4","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_MGTEOS01_4","GLOBAL",2)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_MGTEOS01_4","GLOBAL",2)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* Brandock got apprentices assigned */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!See("MGTEOS01")
	GlobalGT("GettingApprentices","GLOBAL",2)
	Global("ApprentiOver","GLOBAL",0)
	!See("MGAPPR01")
	!See("MGAPPR02")
	!See("MGAPPR03")
	GlobalGT("Apprenti","GLOBAL",0)
	Global("C#Brandock_HasApprentices","GLOBAL",1)
	Global("C#Brandock_MageApprentices","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_MageApprentices","GLOBAL",1)
END

/* Initiate - below */


/* Brandock's teaching is done */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!See("MGTEOS01")
	Global("ApprentiOver","GLOBAL",1)
	!See("MGAPPR01")
	!See("MGAPPR02")
	!See("MGAPPR03")
	Global("C#Brandock_HasApprentices","GLOBAL",1)
	Global("C#Brandock_MageApprentices","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_MageApprentices","GLOBAL",3)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_MageApprentices","GLOBAL",4)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END






/* PC got apprentices assigned */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!See("MGTEOS01")
	GlobalGT("GettingApprentices","GLOBAL",2)
	Global("ApprentiOver","GLOBAL",0)
	See("MGAPPR02")
	GlobalGT("Apprenti","GLOBAL",0)
	Global("C#Brandock_HasApprentices","GLOBAL",0)
	OR(3)
		!Dead("mgappr01")
		!Dead("mgappr02")
		!Dead("mgappr03")
	Global("C#Brandock_MageApprentices","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_MageApprentices","GLOBAL",1)
END

/* Initiate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_MageApprentices","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* made wrong decision */
/* Activate */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	!See("MGTEOS01")
	GlobalGT("Apprenti","GLOBAL",6)	
	Global("C#Brandock_HasApprentices","GLOBAL",0)
	OR(4)
		Global("AllGoingToDie","GLOBAL",1)
		Global("KillLarz","GLOBAL",1)
		Global("KillNara","GLOBAL",1)
		Global("KillMorul","GLOBAL",1)
	!GlobalTimerExpired("ApprentiBuildTimer","GLOBAL")
	AreaCheck("AR0411")
	Global("C#Brandock_ApprenticeLeave","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_ApprenticeLeave","GLOBAL",1)
END

/* Initiate */
/* Global("C#Brandock_ApprenticeLeave","GLOBAL",1) could also be set via INTERJECT */
IF 
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#Brandock_ApprenticeLeave","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END



//////////////////////////////////////////////////////////////

/* Timer-triggered dialogues BGII
variable starts at "100" upon first joining */


/* to ensure that the dialogue does not fire directly after wake up */
IF
  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  RealGlobalTimerExpired("C#BrandockDialogTimer","GLOBAL")
  PartyRested()
OR(3)
    Global("C#Brandock_Dialog","GLOBAL",201)
    Global("C#Brandock_Dialog","GLOBAL",203)
    Global("C#Brandock_Dialog","GLOBAL",205)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",20)
END


/* FP dialogue activation BG2 */
IF
  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  RealGlobalTimerExpired("C#BrandockDialogTimer","GLOBAL")
  !See([ENEMY])
	!Range([NEUTRAL],10)
  See(Player1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  CombatCounter(0)
    OR(3)
    Global("C#Brandock_Dialog","GLOBAL",201)
    Global("C#Brandock_Dialog","GLOBAL",203)
    Global("C#Brandock_Dialog","GLOBAL",205)
THEN
  RESPONSE #100
    IncrementGlobal("C#Brandock_Dialog","GLOBAL",1)
END

/* FP dialogue triggering: BG2 */
IF
//  GlobalLT("Chapter","GLOBAL",##)

  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !See([ENEMY])
  CombatCounter(0)
  See(Player1) 
  !StateCheck(Myself,CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  OR(3)
    Global("C#Brandock_Dialog","GLOBAL",202)  //
    Global("C#Brandock_Dialog","GLOBAL",204)  //
    Global("C#Brandock_Dialog","GLOBAL",206)  //
THEN
  RESPONSE #100
    RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",1800)
		PlaySong(0)
		PlaySound("C#BRAND")    //Does not exist (yet)
    StartDialogNoSet(Player1)
END
