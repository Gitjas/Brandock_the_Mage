/* Korlasz is defeated */

IF

	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck("C#Brandock",CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	AreaCheck("BD0130")  // 
	!See("bdkorlas")
	OR(2)
		Global("BD_KORLASZ_SURRENDER","GLOBAL",1)
		Dead("bdkorlas")
	Global("C#Brando_SoDKorlaszDefeated","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brando_SoDKorlaszDefeated","GLOBAL",1)
END

IF

	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#Brando_SoDKorlaszDefeated","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

/* note about Sarevok's Sword in Korlash's Tomb (bdshkoro.itm) */
/* activate */
IF
    OR(2) 
	PartyHasItem("bdshkoro")
	PartyHasItem("bdshkorj")
    OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
	Global("C#Brando_bdshkoro","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("C#Brando_bdshkoro","LOCALS",1)
END

/* initiate */
IF 
  OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
  Global("C#Brando_bdshkoro","LOCALS",1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
THEN
	RESPONSE #100
    		RealSetGlobalTimer("C#BrandockDialogSpaceTimer","GLOBAL",300)
		StartDialogueNoSet(Player1)
END

/* lore path Korlasz' Crypt */
/* Korlasz */
IF 
Dead("bdkorlas")
See("bdkorlas")
Global("C#Br_LorePathbdkorlasSoD","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathbdkorlasSoD","GLOBAL",1)
DisplayStringHead("C#Brandock",@1292)
ReallyForceSpellRES("c#brlob1",Myself) 
SetInterrupt(TRUE)
END

/* Korlasz' crypt: bd0130: first library */
IF 
AreaCheck("bd0130")
Range("Table_library",15)
Global("C#Br_LorePathbd0120_1SoD","MYAREA",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathbd0120_1SoD","MYAREA",1)
DisplayStringHead("C#Brandock",@1290)
ReallyForceSpellRES("c#brlob2",Myself) 
SetInterrupt(TRUE)
END

/* Korlasz' crypt: bd0130: hidden library */
IF 
AreaCheck("bd0130")
Range("Bookcase_secret01",15)
Global("C#Br_LorePathbd0120_2SoD","MYAREA",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathbd0120_2SoD","MYAREA",1)
DisplayStringHead("C#Brandock",@1289)
ReallyForceSpellRES("c#brlob2",Myself) 
SetInterrupt(TRUE)
END