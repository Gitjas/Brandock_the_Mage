/* Firebeads desk. Dialogue didn't always trigger so I'll put it here. Variable is set by clicking on Firebead's desk (c#bredsk.baf) */
IF
	InMyArea(Player1)
	InMyArea("firebe")
	!StateCheck("firebe",CD_STATE_NOTVALID)
	InMyArea("C#Brandock")
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck("C#Brandock",CD_STATE_NOTVALID)
	Global("C#Br_ScribeScrollQuest","GLOBAL",2)
THEN
	RESPONSE #100
		ActionOverride("firebe",StartDialogNoSet("C#Brandock"))
END

IF
	AreaCheck("%Beregost_FirebeadsHouse_L1%")
	InMyArea(Player1)
	!Dead("firebe")
	!StateCheck("C#Brandock",CD_STATE_NOTVALID)
	OR(2) InParty("C#Brandock") Global("C#BrandockJoined","GLOBAL",2)
	Global("C#Br_ScribeScrollQuest","GLOBAL",4)
	!Global("C#Brando_BookquestHold","GLOBAL",1)
	OR(2)
		Global("c#brscrl","GLOBAL",1)
		Global("c#brscrl","GLOBAL",4)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		StartDialogNoSet("C#Brandock")
		SetInterrupt(TRUE)
END

/* scroll scribing: detect whether it's a new day */
/* we use the "7th party member restdetection" for this: increase variable once so script block in baldur.bcs gets executed */
IF 	
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	!See([ENEMY])
	CombatCounter(0)
	GlobalGT("C#Br_BookRestore","GLOBAL",0) //Brandock scribes scrolls at Firebead's desk
	Global("C#Br_GiveScribeAbility","LOCALS",0)
	Global("C#Br_ScrollRestDetection","LOCALS",0)
THEN
  RESPONSE #100
	IncrementGlobal("C#7thPartyMemberRestCounter","GLOBAL",1)
	SetGlobal("C#Br_ScrollRestDetection","LOCALS",1)
END

IF
	GlobalGT("C#Br_GiveScribeAbility","LOCALS",0)
	Global("C#Br_ScrollRestDetection","LOCALS",1)
THEN
  RESPONSE #100
	IncrementGlobal("C#7thPartyMemberRestCounter","GLOBAL",-1)
	SetGlobal("C#Br_ScrollRestDetection","LOCALS",2)
END