/* lore path: Temple of Oghma AR0319 */
/* activation */
IF %IT_IS_BGII%
AreaCheck("AR0319")
Global("C#Br_LorePathOghmaBG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("C#Br_LorePathOghmaBG2","GLOBAL",1)
END

/* Initiate */
IF %IT_IS_BGII%
Global("C#Br_LorePathOghmaBG2","GLOBAL",1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

/* D'Arnise Stronghold */
/* destroyed keep */
IF %IT_IS_BGII%
!Global("C#Brandock_Shapeshift","GLOBAL",5)
AreaCheck("AR1303")
Range("Tran1300b",20)
!Dead("TorGal")
Global("C#Br_LorePathDArnis1BG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
DisplayStringHead("C#Brandock",@3509)
SetGlobal("C#Br_LorePathDArnis1BG2","GLOBAL",1)
END

/* freed keep (still in area AR1303) */
/* activation */
IF %IT_IS_BGII%
AreaCheck("AR1303")
Range("Tran1300b",20)
Dead("TorGal")
GlobalLT("C#Br_LorePathDArnis1BG2","GLOBAL",2)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
IncrementGlobal("C#Br_LorePathDArnis1BG2","GLOBAL",2)
END
/* freed keep (Stronghold area AR1307) */
/* activation */
IF %IT_IS_BGII%
AreaCheck("AR1307")
Range("Tran1304b",20)
GlobalLT("C#Br_LorePathDArnis1BG2","GLOBAL",2)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
IncrementGlobal("C#Br_LorePathDArnis1BG2","GLOBAL",2)
END

/* Initiate */
IF %IT_IS_BGII%
OR(2)
Global("C#Br_LorePathDArnis1BG2","GLOBAL",2)
Global("C#Br_LorePathDArnis1BG2","GLOBAL",3)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

/* lore path: Bodhi's library AR1513 */
/* activation */
IF %IT_IS_BGII%
AreaCheck("AR1513")
Range("Container 3",15)
Range("Container 4",15)
Range("Container 5",15)
OpenState("Door07",TRUE)
Global("C#Br_LorePathBodhisBG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("C#Br_LorePathBodhisBG2","GLOBAL",1)
END

/* Initiate */
IF %IT_IS_BGII%
Global("C#Br_LorePathBodhisBG2","GLOBAL",1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

/* mage "Dracandros", bridge district */
IF %IT_IS_BGII%
Dead("demmag")
See("demmag")
Global("C#Br_LorePathDracandrosBG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathDracandrosBG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3510)
ReallyForceSpellRES("c#brlob1",Myself) 
SetInterrupt(TRUE)
END

/* mage, Planar Prison */
IF %IT_IS_BGII%
Dead("pbhunt03")
See("pbhunt03")
Global("C#Br_LorePathpbhunt03BG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathpbhunt03BG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3511)
ReallyForceSpellRES("c#brlob1",Myself) 
SetInterrupt(TRUE)
END

/* mage "Gaius", sewers */
IF %IT_IS_BGII%
Dead("gaius")
See("gaius")
Global("C#Br_LorePathgaiusBG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathgaiusBG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3512)
ReallyForceSpellRES("c#brlob1",Myself)
SetInterrupt(TRUE)
END

/* mage "Sion", Guarded Compound Temple District */
IF %IT_IS_BGII%
Dead("hlsion")
See("hlsion")
Global("C#Br_LorePathhlsionBG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathhlsionBG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3513)
ReallyForceSpellRES("c#brlob1",Myself)
SetInterrupt(TRUE)
END

/* Xzar, Docks District */
IF %IT_IS_BGII%
Dead("lyros")
See("lyros")
Global("C#Br_LorePathlyrosBG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathlyrosBG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3514)
ReallyForceSpellRES("c#brlob1",Myself) 
SetInterrupt(TRUE)
END

/* Jaheira's quest: Bessen und Nadinal */
/* Dead("JAGA1") Dead("JAGA3") */
IF %IT_IS_BGII%
Dead("JAGA1") Dead("JAGA3")
OR(2) See("JAGA1") See("JAGA3")
Global("C#Br_LorePathJAGABG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathJAGABG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3515)
SetInterrupt(TRUE)
END

/* Planar Sphere: Halfling mage */
IF %IT_IS_BGII%
Dead("obshal03")
See("obshal03")
Global("C#Br_LorePathobshal03BG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathobshal03BG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@1030)
SetInterrupt(TRUE)
END

/* Planar Sphere: Kayardi */
IF %IT_IS_BGII%
Dead("obshal05")
See("obshal05")
Global("C#Br_LorePathobshal03BG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathobshal03BG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3517)
ReallyForceSpellRES("c#brlob1",Myself)
SetInterrupt(TRUE)
END

/* Lavok... */
IF %IT_IS_BGII%
Dead("LAVOK02")
See("LAVOK02")
Global("C#Br_LorePathLAVOK02BG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathLAVOK02BG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3518)
ReallyForceSpellRES("c#brlob1",Myself)
SetInterrupt(TRUE)
END



/* Watcher's Keep: Brother Pol */
IF %IT_IS_BGII%
Dead("shupol01")
See("shupol01")
Global("C#Br_LorePathshupol01BG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathshupol01BG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3516)
SetInterrupt(TRUE)
END

/* random encounters: Slaver Wizard (remage01) */
IF %IT_IS_BGII%
Dead("remage01")
See("remage01")
Global("C#Br_LorePathremage01BG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathremage01BG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3520)
SetInterrupt(TRUE)
END

/* random encounters: Mage (rethug02) */
IF %IT_IS_BGII%
Dead("rethug02")
See("rethug02")
Global("C#Br_LorePathrethug02BG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathrethug02BG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3519)
SetInterrupt(TRUE)
END

/* Mage Kaol, Windspear Hills, Firkraag's Dungeon */
IF %IT_IS_BGII%
Dead("kaol")
See("kaol")
Global("C#Br_LorePathkaolBG2","GLOBAL",0)
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
//!See([ENEMY])
THEN
RESPONSE #100
SetInterrupt(FALSE)
SetGlobal("C#Br_LorePathkaolBG2","GLOBAL",1)
DisplayStringHead("C#Brandock",@3520)
SetInterrupt(TRUE)
END

/* Perth in Brynnlaw */
/* --> in normal BG2 baf and d */



/* after killing Irenicus - will only trigger as own dialogue if some sort of "Endless SoA" mod is active */
/* activation */
IF %IT_IS_BGII%
RealGlobalTimerExpired("C#BrandockDialogTimer","GLOBAL")
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
Global("C#Brandock_IrenicusSpellbook","GLOBAL",0)
GlobalLT("Chapter","GLOBAL",%bg2_chapter_8%) //we are not in ToB
OR(2)
	Dead("helljon")
	Dead("helljon2")
THEN
RESPONSE #100
    RealSetGlobalTimer("C#BrandockDialogTimer","GLOBAL",C#BRANDOCK_TIMERSHORT)
SetGlobal("C#Brandock_IrenicusSpellbook","GLOBAL",1)
END

/* Initiate */
IF %IT_IS_BGII%
Global("C#Brandock_IrenicusSpellbook","GLOBAL",1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


/* after killing Ilasera ## surely needs more triggers to time it better */
/* activation */
IF %IT_IS_BGII%
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
GlobalLT("C#Brandock_LorePathStop","GLOBAL",3)
Dead("illasera")
THEN
RESPONSE #100
SetGlobal("C#Brandock_LorePathStop","GLOBAL",3)
END

/* Initiate */
IF %IT_IS_BGII%
Global("C#Brandock_LorePathStop","GLOBAL",3)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

/* lore path: Lazarus' Spellbook (Saradush) */
/* activation */
IF %IT_IS_BGII%
OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
PartyHasItem("PLOT18B")
Global("C#Br_LorePathLazarusBG2","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("C#Br_LorePathLazarusBG2","GLOBAL",1)
END

/* Initiate */
IF %IT_IS_BGII%
Global("C#Br_LorePathLazarusBG2","GLOBAL",1)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END