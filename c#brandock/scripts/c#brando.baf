/* Brandock's main script - valid for BG1 and BGII */

/* in case Brandock follows as 7th party member and Player1 was rested via script and not via  RestParty() */

/* spell and priciple taken from Zed Nocear's BG1 Trigger Simulations! */

IF
	Global("C#BrandockRestCounter","GLOBAL",1)
	RealGlobalTimerExpired("C#7thPartyMemberRestAfterTimer","GLOBAL")
	CombatCounter(0)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("C#BrandockRestCounter","GLOBAL",0)
END

IF
	Global("C#BrandockRestCounter","GLOBAL",0)
	!RealGlobalTimerExpired("C#7thPartyMemberRestAfterTimer","GLOBAL")
	Global("C#BrandockJoined","GLOBAL",2)
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("C#BrandockRestCounter","GLOBAL",1)
		Rest()
		ReallyForceSpellRES("C#BRREST",Myself)
		SetInterrupt(TRUE)	
END



/* follows as familiar: reset variable for refreshing of min HP spell after fight */

IF
	Global("C#Brandock_Has1HPEffect","GLOBAL",2)
	Global("C#BrandockJoined","GLOBAL",2)  //follows as 7th party member
	CombatCounter(0)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_Has1HPEffect","GLOBAL",1)
END

/* refresh min HP spell before a fight */
IF
	Global("C#Brandock_Has1HPEffect","GLOBAL",1)
	Global("C#BrandockJoined","GLOBAL",2)  //follows as 7th party member
	See([ENEMY])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("C#Brandock_Has1HPEffect","GLOBAL",2)
		ReallyForceSpellRES("C#MIN5HP","C#Brandock")
		SetInterrupt(TRUE)
END


/* follows as familiar: fight is over - Brandock had passed out at 1 HP. Regains consciousness and gets healing boost to be ready to be taken into the group and not die upon joining the group */

IF
	Global("C#Brandock_IsUnconscious","GLOBAL",1)
	Global("C#BrandockJoined","GLOBAL",2)  //follows as 7th party member
	CombatCounter(0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		ReallyForceSpellRES("C#BRREST","C#Brandock")    // Cures effects and restores health - taken from Argent77's Djinni Companion with many thanks	
		SetGlobal("C#Brandock_IsUnconscious","GLOBAL",0)		
		DisplayStringHead(Myself,@1001)
		MakeGlobal()
		ChangeEnemyAlly(Myself, FAMILIAR)
		AddFamiliar()	
		ActionOverride("C#Brandock",SetDialog("c#brandj"))
		ChangeAIScript("DEFAULT",CLASS)
		ChangeAIScript("",RACE)
		ChangeAIScript("",GENERAL)
		ChangeAIScript("",DEFAULT)		
		DisplayStringHead(Myself,@1002)
		SetInterrupt(TRUE)
END

/* follows as familiar: fight ends - resetting health warn variable */
IF
	Global("C#Brandock_HPAlarmDone","LOCALS",1)
	Global("C#BrandockJoined","GLOBAL",2)  //follows as 7th party member
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#Brandock_HPAlarmDone","LOCALS",0)
END

/* follows as familiar: Brandock warns about HP too low: auto pause. This can be switsched on and off via PID */

IF
	Global("C#Brandock_HPAlarm","GLOBAL",1)  //PC told Brandock to warn
	Global("C#Brandock_HPAlarmDone","LOCALS",0)
	Global("C#BrandockJoined","GLOBAL",2)  //follows as 7th party member
	!CombatCounter(0)
	HPPercentLT(Myself,45)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@1003)
		SetGlobal("C#Brandock_HPAlarmDone","LOCALS",1)
		PauseGame()
END

/* follows as familiar: Brandock warns about HP too low: no auto pause. This can be switsched on and off via PID */

IF
	Global("C#Brandock_HPAlarm","GLOBAL",0)  //PC told Brandock not to warn
	Global("C#Brandock_HPAlarmDone","LOCALS",0)
	Global("C#BrandockJoined","GLOBAL",2)  //follows as 7th party member
	!CombatCounter(0)
	HPPercentLT(Myself,45)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@1003)
		SetGlobal("C#Brandock_HPAlarmDone","LOCALS",1)
END



/* follows as familiar: Brandock passes out if at 1 HP and following as a 7th party member (otherwise he would be a killing machine) */

IF
	Global("C#Brandock_IsUnconscious","GLOBAL",0)
	Global("C#BrandockJoined","GLOBAL",2)  //follows as 7th party member
	!CombatCounter(0)
	Range(NearestEnemyOf(Myself),10)
	HPLT(Myself,6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		DisplayStringHead(Myself,@1004) 
		ClearActions(Myself)
		RemoveFamiliar()
		ChangeEnemyAlly(Myself,NEUTRAL)
		ChangeAIScript("",CLASS)
		ChangeAIScript("",RACE)
		ChangeAIScript("",GENERAL)
		ChangeAIScript("",DEFAULT)	
		SetGlobal("C#Brandock_IsUnconscious","GLOBAL",1)
		SetInterrupt(TRUE)
END

/* follows as familiar: Brandock is at 1 HP and passes out */
IF
	Global("C#Brandock_IsUnconscious","GLOBAL",1)
	!CombatCounter(0)
THEN
	RESPONSE #100
		PlayDead(100)
END




/* following as familiar/7th party member. Give 1 HP effect */

IF
	!InParty(Myself)
	Global("C#BrandockJoined","GLOBAL",2)
	Global("C#Brandock_Has1HPEffect","GLOBAL",0)
THEN
  RESPONSE #100
		SetInterrupt(FALSE)
		ReallyForceSpellRES("C#BRREST","C#Brandock") //make sure there is no negative effect
		ReallyForceSpellRES("C#MIN5HP","C#Brandock") //give him min 5 HP and immunities
		SetGlobal("C#Brandock_Has1HPEffect","GLOBAL",1)
		IncrementGlobal("C#7thPartyMemberRestCounter","GLOBAL",1) //needed for Brandock's spell regeneration
		SetInterrupt(TRUE)	
END


/* Brandock joined the party as full member. remove 1 HP effect */

IF
	InParty(Myself)
	!Global("C#Brandock_Has1HPEffect","GLOBAL",0)
THEN
  RESPONSE #100
		SetInterrupt(FALSE)
		ReallyForceSpellRES("C#MIN0HP","C#Brandock")
		SetGlobal("C#Brandock_Has1HPEffect","GLOBAL",0)	
		IncrementGlobal("C#7thPartyMemberRestCounter","GLOBAL",-1)
		SetInterrupt(TRUE)
END


/* more general stuff. Could happen in BG1 (SoD) or BGII */

/* start banter script if not in party */

IF
	Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	InMyArea(Player1)  
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	RealGlobalTimerExpired("C#BrandockNPCBanterTimer","GLOBAL")
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#BrandockNPCBanterTimer","GLOBAL",3000)
		Interact(Player1)
END




