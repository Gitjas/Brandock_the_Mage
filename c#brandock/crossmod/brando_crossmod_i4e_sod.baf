/* Brandock - Imoen banter, SoD style */

/* activate timer for Imoen banter */
IF
	%IT_IS_SOD%
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	InParty("%IMOEN_DV%")  
	OR(4)
		Global("C#Brandock_ImoenSoD","GLOBAL",0) 
		Global("C#Brandock_ImoenSoD","GLOBAL",3) 
		Global("C#Brandock_ImoenSoD","GLOBAL",6)
		Global("C#Brandock_ImoenSoD","GLOBAL",9)
THEN
	RESPONSE #100
		SetGlobalTimerRandom("bd_banter_Imoen_trigger","LOCALS",ONE_DAY,FOURTEEN_DAYS)
		IncrementGlobal("C#Brandock_ImoenSoD","GLOBAL",1)
END

/* deactivate timer if Imoen or Brandock is not in party */
IF
	%IT_IS_SOD%
	OR(2)
		Global("C#BrandockJoined","GLOBAL",0)
		!InParty("%IMOEN_DV%")  
	OR(4)
		Global("C#Brandock_ImoenSoD","GLOBAL",1)
		Global("C#Brandock_ImoenSoD","GLOBAL",4)
		Global("C#Brandock_ImoenSoD","GLOBAL",7)
		Global("C#Brandock_ImoenSoD","GLOBAL",10)
THEN
	RESPONSE #100
		IncrementGlobal("C#Brandock_ImoenSoD","GLOBAL",-1)
END

/* first & 4th Imoen banter */
/* activation */
IF
	%IT_IS_SOD%
	OR(2) 
		Global("C#Brandock_SoDBanterDebugCM","GLOBAL",1) 
		GlobalTimerExpired("bd_banter_Imoen_trigger","LOCALS") 
	OR(2) 
		Global("C#Brandock_SoDBanterDebugCM","GLOBAL",1) 
		!GlobalTimerNotExpired("BD_NPC_BANTER","GLOBAL")
	GlobalTimerNotExpired("BD_AREA_BANTER_DELAY","MYAREA")
	OR(2)
		Global("C#Brandock_ImoenSoD","GLOBAL",1)
		Global("C#Brandock_ImoenSoD","GLOBAL",10)
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See("%IMOEN_DV%")
	IfValidForPartyDialogue("%IMOEN_DV%")  
	!PartyRested()
	!ActuallyInCombat()
THEN
	RESPONSE #100
		BanterBlockTime(450) 
		SetGlobalTimer("BD_NPC_BANTER","GLOBAL",FOUR_HOURS)
		IncrementGlobal("C#Brandock_ImoenSoD","GLOBAL",1)
END

/* first Imoen banter */
/* initiation */
IF
	%IT_IS_SOD%
	Global("C#Brandock_ImoenSoD","GLOBAL",2)
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See("%IMOEN_DV%")
	IfValidForPartyDialogue("%IMOEN_DV%")  
THEN
	RESPONSE #100
		SetAreaScript("c#brsc01",GENERAL)
END

/* 4th Imoen banter */
/* initiation */
IF
	%IT_IS_SOD%
	Global("C#Brandock_ImoenSoD","GLOBAL",11)
	OR(2) InParty(Myself) Global("C#BrandockJoined","GLOBAL",2)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See("%IMOEN_DV%")
	IfValidForPartyDialogue("%IMOEN_DV%")  
THEN
	RESPONSE #100
		SetAreaScript("c#brsc04",GENERAL)
END

